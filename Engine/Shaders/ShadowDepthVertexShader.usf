/*=============================================================================
	ShadowDepthVertexShader.usf: Vertex shader for writing shadow depth.
	Copyright 1998-2013 Epic Games, Inc. All Rights Reserved.
=============================================================================*/

#include "Common.usf"
#include "Material.usf"
#include "VertexFactory.usf"
#include "ShadowDepthCommon.usf"

float4x4 ProjectionMatrix;

//@todo - make this a bool, bool shader parameters currently do not work in vertex shaders on Xbox 360 (TTP 125134)
float bClampToNearPlane;

void SetShadowDepthOutputs(float4 WorldPosition, out float4 OutPosition, out float ShadowDepth)
{
	OutPosition = MulMatrix(ProjectionMatrix, WorldPosition);

	// Clamp the vertex to the near plane if it is in front of the near plane
	// This has problems if some vertices of a triangle get clamped and others do not, also causes artifacts with non-ortho projections
	if (bClampToNearPlane && OutPosition.z < 0)
	{
		OutPosition.z = 0.000001f;
		OutPosition.w = 1.0f;
	}

	#if PERSPECTIVE_CORRECT_DEPTH
		ShadowDepth = OutPosition.z;
	#else
		// Output linear, normalized depth
		ShadowDepth = (OutPosition.z * InvMaxSubjectDepth + DepthBias);
		#if SUPPORTS_DEPTH_TEXTURES
			OutPosition.z = ShadowDepth * OutPosition.w;
		#else
			#if !OUTPUT_DEPTH_TO_COLOR
				OutPosition.z = ShadowDepth * OutPosition.w;
			#endif
		#endif
	#endif
}

#if SM5_PROFILE && ONEPASS_POINTLIGHT_SHADOW

/** View projection matrix for each cube map face. */
float4x4 ShadowViewProjectionMatrices[6];
/** Flag indicating which faces of the cube map the object is visible to.  This just needs 6 bits, but there's no int parameter type yet. */
float4 MeshVisibleToFace[6];

#define FPassSpecificVSToGS FShadowDepthVSToPS

struct FShadowDepthGSToPS
{
#if !MATERIALBLENDING_SOLID
	FShadowDepthVSToPS PSInputs;
#endif

	/** Controls which of the cube map faces to rasterize the primitive to, only the value from the first vertex is used. */
	uint RTIndex : SV_RenderTargetArrayIndex;
	float4 OutPosition : POSITION;
};
 
/** Allocate space for cloning to all 6 faces which is the worst case. */
[maxvertexcount(18)]
void MainOnePassPointLightGS(triangle FPassSpecificVSToGS Input[3], inout TriangleStream<FShadowDepthGSToPS> OutStream)
{
	UNROLL
	// Clone the triangle to each face
    for (int CubeFaceIndex = 0; CubeFaceIndex < 6; CubeFaceIndex++)
    {
		BRANCH
		// Skip this cube face if the object is not visible to it
		if (MeshVisibleToFace[CubeFaceIndex].x > 0)
		{
			float4 ClipSpacePositions[3];
			UNROLL
			for (int VertexIndex = 0; VertexIndex < 3; VertexIndex++)
			{
				// Calculate the clip space position for each cube face
				ClipSpacePositions[VertexIndex] = MulMatrix(ShadowViewProjectionMatrices[CubeFaceIndex], Input[VertexIndex].GSPosition);
			}

			float4 FrustumTests0 = saturate(ClipSpacePositions[0].xyxy * float4(-1, -1, 1, 1) - ClipSpacePositions[0].w);		
			float4 FrustumTests1 = saturate(ClipSpacePositions[1].xyxy * float4(-1, -1, 1, 1) - ClipSpacePositions[1].w);		
			float4 FrustumTests2 = saturate(ClipSpacePositions[2].xyxy * float4(-1, -1, 1, 1) - ClipSpacePositions[2].w);		
			float4 FrustumTests = FrustumTests0 * FrustumTests1 * FrustumTests2;		

			BRANCH		
			// Frustum culling, saves GPU time with high poly meshes
			if (!any(FrustumTests))		
			{				
				FShadowDepthGSToPS Output;
				Output.RTIndex = CubeFaceIndex;

				UNROLL
				for (int VertexIndex = 0; VertexIndex < 3; VertexIndex++)
				{
					Output.OutPosition = ClipSpacePositions[VertexIndex];

					#if !MATERIALBLENDING_SOLID
						Output.PSInputs.FactoryInterpolants = Input[VertexIndex].FactoryInterpolants;
						Output.PSInputs.GSPosition = Output.OutPosition;
					#endif

					OutStream.Append(Output);
				}
				OutStream.RestartStrip();
			}
		}
	}
}

#endif

#if USING_SM5_TESSELLATION

struct FShadowDepthVSToDS
{
	FVertexFactoryInterpolantsVSToDS FactoryInterpolants;
	float4 Position : VS_to_DS_Position;
	OPTIONAL_VertexID_VS_To_DS
};

struct FShadowDepthDSToPS : FShadowDepthVSToPS
{
	float4 Position : POSITION;
};

#define FPassSpecificVSToDS FShadowDepthVSToDS
#define FPassSpecificVSToPS FShadowDepthDSToPS

FShadowDepthVSToDS PassInterpolate(FShadowDepthVSToDS a, float aInterp, FShadowDepthVSToDS b, float bInterp)
{
	FShadowDepthVSToDS O;
	O.FactoryInterpolants = VertexFactoryInterpolate(a.FactoryInterpolants, aInterp, b.FactoryInterpolants, bInterp);
	return O;
}

FShadowDepthDSToPS PassFinalizeTessellationOutput(FShadowDepthVSToDS Interpolants, float4 WorldPosition, FMaterialTessellationParameters MaterialParameters)
{
	FShadowDepthDSToPS O;
	
#if !MATERIALBLENDING_SOLID
	O.FactoryInterpolants = VertexFactoryAssignInterpolants(Interpolants.FactoryInterpolants);
#endif


#if ONEPASS_POINTLIGHT_SHADOW
	O.Position = O.GSPosition = WorldPosition;
#else

	float ShadowDepth;
	SetShadowDepthOutputs(
		WorldPosition, 
		O.Position, 
#if PERSPECTIVE_CORRECT_DEPTH || !SUPPORTS_DEPTH_TEXTURES
		O.ShadowDepth
#else
		ShadowDepth
#endif
		);
#endif

#if !(ONEPASS_POINTLIGHT_SHADOW || PERSPECTIVE_CORRECT_DEPTH || !SUPPORTS_DEPTH_TEXTURES) && COMPILER_SUPPORTS_EMPTY_STRUCTS
	O.Dummy = 0;
#endif

	return O;
}
	
#include "Tessellation.usf"

#endif // #if USING_SM5_TESSELLATION

void Main(
	FVertexFactoryInput Input,
#if USING_SM5_TESSELLATION
	out FShadowDepthVSToDS OutParameters,
#else
	out FShadowDepthVSToPS OutParameters,
#endif 	
	OPTIONAL_VertexID
	out float4 OutPosition : POSITION
	)
{
	FVertexFactoryIntermediates VFIntermediates = GetVertexFactoryIntermediates(Input);
	float4 WorldPos = VertexFactoryGetWorldPosition(Input, VFIntermediates);
	float3x3 TangentBasis = VertexFactoryGetTangentBasis(Input, VFIntermediates);

	FMaterialVertexParameters VertexParameters = GetMaterialVertexParameters(Input, VFIntermediates, WorldPos.xyz, TangentBasis);
	WorldPos.xyz += GetMaterialWorldPositionOffset(VertexParameters);

#if USING_SM5_TESSELLATION	
	OutPosition = OutParameters.Position = WorldPos;
	
	// Tessellated materials need vertex coords for displacement
	OutParameters.FactoryInterpolants = VertexFactoryGetInterpolantsVSToDS(Input, VFIntermediates);
	
#elif ONEPASS_POINTLIGHT_SHADOW

	OutPosition = OutParameters.GSPosition = WorldPos;

	#if !MATERIALBLENDING_SOLID
		// Masked materials need texture coords to clip
		OutParameters.FactoryInterpolants = VertexFactoryGetInterpolantsVSToPS(Input, VFIntermediates);
	#endif

#else

	float Dummy;

	SetShadowDepthOutputs(
		WorldPos, 
		OutPosition, 
#if !PERSPECTIVE_CORRECT_DEPTH && SUPPORTS_DEPTH_TEXTURES
		Dummy
#else
		OutParameters.ShadowDepth
#endif
		);
	
	#if !MATERIALBLENDING_SOLID
		// Masked materials need texture coords to clip
		OutParameters.FactoryInterpolants = VertexFactoryGetInterpolantsVSToPS(Input, VFIntermediates);
	#endif

	#if !PERSPECTIVE_CORRECT_DEPTH && SUPPORTS_DEPTH_TEXTURES && !COMPILER_SUPPORTS_EMPTY_STRUCTS
		OutParameters.Dummy = 0;
	#endif
#endif

	OutputVertexID( OutParameters );
}
