/*=============================================================================
	DepthOnlyVertexShader.hlsl: Depth-only vertex shader.
	Copyright 1998-2013 Epic Games, Inc. All Rights Reserved.
=============================================================================*/

#include "Common.usf"
#include "Material.usf"
#include "VertexFactory.usf"

struct FDepthOnlyVSToPS
{
	#if !MATERIALBLENDING_SOLID
		FVertexFactoryInterpolantsVSToPS FactoryInterpolants;
		float4 CameraVectorOrVertexColor : TEXCOORD4;
		float4 PixelPosition : TEXCOORD5;
	#endif

	float4 Position : POSITION;
};

#if USING_SM5_TESSELLATION	
	struct FDepthOnlyVSToDS
	{
		FVertexFactoryInterpolantsVSToDS FactoryInterpolants;
	#if !MATERIALBLENDING_SOLID && PER_PIXEL_CAMERA_VECTOR
		float4 CameraVectorOrVertexColor : TEXCOORD4;
	#endif
		float4 Position : VS_To_DS_Position;
		OPTIONAL_VertexID_VS_To_DS
	};
	
	#define FDepthOnlyVSOutput FDepthOnlyVSToDS
	#define VertexFactoryGetInterpolants VertexFactoryGetInterpolantsVSToDS
#else
	#define FDepthOnlyVSOutput FDepthOnlyVSToPS
	#define VertexFactoryGetInterpolants VertexFactoryGetInterpolantsVSToPS
#endif

#if USING_SM5_TESSELLATION	
	#define FPassSpecificVSToDS FDepthOnlyVSToDS
	#define FPassSpecificVSToPS FDepthOnlyVSToPS
	
	FDepthOnlyVSToDS PassInterpolate(FDepthOnlyVSToDS a, float aInterp, FDepthOnlyVSToDS b, float bInterp)
	{
		FDepthOnlyVSToDS O;
		
		O.FactoryInterpolants = VertexFactoryInterpolate(a.FactoryInterpolants, aInterp, b.FactoryInterpolants, bInterp);
		
		#if !MATERIALBLENDING_SOLID && PER_PIXEL_CAMERA_VECTOR
			// Interpolate vertex color
			TESSELLATION_INTERPOLATE_MEMBER(CameraVectorOrVertexColor);
		#endif

		return O;
	}

	FDepthOnlyVSToPS PassFinalizeTessellationOutput(FDepthOnlyVSToDS Interpolants, float4 WorldPosition, FMaterialTessellationParameters MaterialParameters)
	{
		FDepthOnlyVSToPS O;
		
		#if !MATERIALBLENDING_SOLID
			O.FactoryInterpolants = VertexFactoryAssignInterpolants(Interpolants.FactoryInterpolants);
		#endif
		
		#if !MATERIALBLENDING_SOLID 
			#if PER_PIXEL_CAMERA_VECTOR
				O.CameraVectorOrVertexColor = MaterialParameters.VertexColor;	
			#else
				O.CameraVectorOrVertexColor.xyz = TransformWorldVectorToTangent(MaterialParameters.TangentToWorld,CameraPositionDS.xyz - WorldPosition.xyz * CameraPositionDS.w);
				O.CameraVectorOrVertexColor.w = 1.0f;
			#endif
		#endif

		// Finally, transform position to clip-space
		ISOLATE
		{
			O.Position = MulMatrix(ViewProjectionMatrixDS,WorldPosition);
			O.Position = ApplyTemporalAAJitter(O.Position);
		}
			
		#if !MATERIALBLENDING_SOLID
			#if WORLD_POS
				O.PixelPosition = WorldPosition;
			#else
				O.PixelPosition = O.Position;
			#endif
		#endif
		
		return O;
	}
	
	#include "Tessellation.usf"
#endif

#if VERTEXSHADER

void Main(
	FVertexFactoryInput Input,
	OPTIONAL_VertexID
	out FDepthOnlyVSOutput Output
	)
{
	FVertexFactoryIntermediates VFIntermediates = GetVertexFactoryIntermediates(Input);
	float4 WorldPos = VertexFactoryGetWorldPosition(Input, VFIntermediates);

#if TERRAIN_VERTEX_FACTORY
	// Don't calculate TangentBasis here for terrain, as doing so causes self-z-fighting on xbox 360, regardless of any ISOLATE usage
	FMaterialVertexParameters VertexParameters = GetMaterialVertexParameters(Input, VFIntermediates, WorldPos.xyz, float3x3(float3(0,0,0),float3(0,0,0),float3(0,0,0)));
#else
	float3x3 TangentBasis = VertexFactoryGetTangentBasis(Input, VFIntermediates);
	FMaterialVertexParameters VertexParameters = GetMaterialVertexParameters(Input, VFIntermediates, WorldPos.xyz, TangentBasis);
#endif
	// Isolate instructions used for world position offset on xbox 360, 
	// As these cause the optimizer to generate different position calculating instructions in each pass, resulting in self-z-fighting.
	// This is only necessary for shaders used in passes that have depth testing enabled.
	ISOLATE
	{
		WorldPos.xyz += GetMaterialWorldPositionOffset(VertexParameters);
	}

#if USING_SM5_TESSELLATION
	// Transformation is done in Domain shader when tessellating
	Output.Position = WorldPos;
#else
#if WITH_REALD
	FDepthAllocateOutput DBAResult;
#endif
	ISOLATE
	{
#if WITH_REALD
		DBAResult = DepthAllocate(WorldPos);
		Output.Position = DBAResult.DBAPixelPosition;
#else
		Output.Position = MulMatrix(ViewProjectionMatrix,WorldPos);
#endif
		Output.Position = ApplyTemporalAAJitter(Output.Position);
	}
#endif

#if !MATERIALBLENDING_SOLID || USING_SM5_TESSELLATION
	// Masked and transparent materials need texture coords to clip, and tessellated
	// materials need texture coords to displace
	Output.FactoryInterpolants = VertexFactoryGetInterpolants(Input, VFIntermediates);
#endif

#if !MATERIALBLENDING_SOLID
	#if !USING_SM5_TESSELLATION	
		#if WORLD_POS
			Output.PixelPosition = WorldPos;
		#else
#if WITH_REALD
			Output.PixelPosition = DBAResult.DBAPixelPosition;
#else
			Output.PixelPosition = Output.Position;
#endif
		#endif
	#endif

#if TERRAIN_VERTEX_FACTORY
	float3x3 TangentBasis = VertexFactoryGetTangentBasis(Input, VFIntermediates);
#endif

#if PER_PIXEL_CAMERA_VECTOR
	Output.CameraVectorOrVertexColor = VertexFactoryGetVertexColor(Input, VFIntermediates);
#elif !USING_SM5_TESSELLATION
	Output.CameraVectorOrVertexColor = 1;
	// The mask may be dependent on the camera vector
	Output.CameraVectorOrVertexColor.xyz = VertexFactoryWorldToTangentSpace(Input,VFIntermediates,TangentBasis,CameraPositionVS.xyz - WorldPos.xyz * CameraPositionVS.w);
#endif

#endif

	OutputVertexID( Output );
}

#endif // VERTEXSHADER
